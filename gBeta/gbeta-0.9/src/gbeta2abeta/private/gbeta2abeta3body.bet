(* FILE "./gbeta2abeta/private/gbeta2abeta4body.bet"
 *
 * Copyright (C) 1997-2001 Erik Ernst
 *
 * This file is part of "gbeta" -- a generalized version of the
 * programming language BETA.
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License as
 * published by the Free Software Foundation; either version 2 of
 * the License, or (at your option) any later version.
 *
 * This program is destributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * Among other things, the copyright notice and this notice must be
 * preserved on all copies.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program, probably in a file named COPYING; if not,
 * write to the Free Software Foundation, Inc., 675 Mass Ave,
 * Cambridge, MA 02139, USA.
 *
 * To contact the author by email: eernst@cs.auc.dk
 *
 * To contact the author by snail-mail:
 *    Erik Ernst
 *    Department of Computer Science
 *    University of Aalborg
 *    DK-9220 Aalborg OE
 *    Denmark
 *
 * $Id: gbeta2abeta3body.bet,v 1.11 2001/07/01 21:44:10 eernst Exp $
 *)

ORIGIN 'gbeta2abetaAutogen';
INCLUDE '~beta/basiclib/numberio';
INCLUDE '../../beta2abeta/private/betaastbody';
INCLUDE '../../utils';
INCLUDE 'gbeta2abeta1body'

-- gBeta2abetaLib:attributes --

gVariablePatternCase: gBeta2abetaPrefix
  (# name::(# do 'VariablePattern'->value[] #);
     node: ^VariablePattern;
     l2vpat: ^interpreterbase.l2VariablePattern
  do mpsAst[]->node[];
     (&ib.l2VariablePattern[]).init->l2vpat[];
     (focus[],node.getAttributeDenotation,AttributeDenotation##)
       ->astOrSlotToLevelTwo
       ->l2vpat.AttributeDenotation[];
     l2vpat[]->l2ast[]
  #);

gStaticItemCase: gBeta2abetaPrefix
  (# name::(# do 'StaticItem'->value[] #);
     node: ^StaticItem;
     theMerge: ^Merge;
     l2sitem: ^interpreterbase.l2StaticItem
  do mpsAst[]->node[];
     (&ib.l2StaticItem[]).init->l2sitem[];
     node.getMerge->theMerge[];
     (focus[],theMerge[],l2sitem.ObjectSpecifications[]
     ,Merge##,ObjectSpecification##)
       ->astlistOrSlotsToLevelTwo;
     theMerge[]->l2sitem.ObjectSpecifications.Identity.mpsAst[];
     l2sitem[]->l2ast[]
  #);

gVirtualStaticItemCase: gBeta2abetaPrefix
  (# name::(# do 'VirtualStaticItem'->value[] #);
     node: ^VirtualStaticItem;
     ignoreDclNode: ^fragmentGraph.node;
     maybeDisown: ^ast;
     l2vsitem: ^interpreterbase.l2VirtualStaticItem
  do mpsAst[]->node[];
     (&ib.l2VirtualStaticItem[]).init->l2vsitem[];
     (* get the a.den. *)
     (focus[],node.getAttributeDenotation,AttributeDenotation##)
       ->astOrSlotToLevelTwo
       ->l2vsitem.AttributeDenotation[];
     (* extract disownment *)
     (focus[],node.getDisownOpt,Disown##)
       ->astOrSlotOrMissingToAst
       ->(ignoreDclNode[],maybeDisown[]);
     (maybeDisown[]<>NONE)->l2vsitem.Disown;
     (* deliver *)
     l2vsitem[]->l2ast[]
  #);

gFinalStaticItemCase: gBeta2abetaPrefix
  (# name::(# do 'FinalStaticItem'->value[] #);
     node: ^FinalStaticItem;
     theMerge: ^Merge;
     l2fsitem: ^interpreterbase.l2FinalStaticItem
  do mpsAst[]->node[];
     (&ib.l2FinalStaticItem[]).init->l2fsitem[];
     node.getMerge->theMerge[];
     (focus[],theMerge[],l2fsitem.ObjectSpecifications[]
     ,Merge##,ObjectSpecification##)
       ->astlistOrSlotsToLevelTwo;
     theMerge[]->l2fsitem.ObjectSpecifications.Identity.mpsAst[];
     l2fsitem[]->l2ast[]
  #);

gDynamicItemCase: gBeta2abetaPrefix
  (# name::(# do 'DynamicItem'->value[] #);
     node: ^DynamicItem;
     maybeExact: ^ast;
     ignoreDclNode: ^fragmentGraph.node;
     l2ditem: ^interpreterbase.l2DynamicItem
  do
     (* init *)
     mpsAst[]->node[];
     (&ib.l2DynamicItem[]).init->l2ditem[];

     (* check whether this is an exact match *)
     (focus[],node.getExactOpt,Exact##)
       ->astOrSlotOrMissingToAst
       ->(ignoreDclNode[],maybeExact[]);
     (maybeExact[]<>NONE)
       ->l2ditem.Exact;

     (* get the declared type *)
     (focus[],node.getAttributeDenotation,AttributeDenotation##)
       ->astOrSlotToLevelTwo
       ->l2ditem.AttributeDenotation[];

     (* deliver *)
     l2ditem[]->l2ast[]
  #);

gStaticComponentCase: gBeta2abetaPrefix
  (# name::(# do 'StaticComponent'->value[] #);
     node: ^StaticComponent;
     theMerge: ^Merge;
     l2scomp: ^interpreterbase.l2StaticComponent
  do mpsAst[]->node[];
     (&ib.l2StaticComponent[]).init->l2scomp[];
     node.getMerge->theMerge[];
     (focus[],theMerge[],l2scomp.ObjectSpecifications[]
     ,Merge##,ObjectSpecification##)
       ->astlistOrSlotsToLevelTwo;
     theMerge[]->l2scomp.ObjectSpecifications.Identity.mpsAst[];
     l2scomp[]->l2ast[]
  #);

gDynamicComponentCase: gBeta2abetaPrefix
  (# name::(# do 'DynamicComponent'->value[] #);
     node: ^DynamicComponent;
     maybeExact: ^ast;
     ignoreDclNode: ^fragmentGraph.node;
     l2dcomp: ^interpreterbase.l2DynamicComponent
  do
     (* init  *)
     mpsAst[]->node[];
     (&ib.l2DynamicComponent[]).init->l2dcomp[];

     (* check whether this is an exact match *)
     (focus[],node.getExactOpt,Exact##)
       ->astOrSlotOrMissingToAst
       ->(ignoreDclNode[],maybeExact[]);
     (maybeExact[]<>NONE)
       ->l2dcomp.Exact;

     (* get the declared type *)
     (focus[],node.getAttributeDenotation,AttributeDenotation##)
       ->astOrSlotToLevelTwo
       ->l2dcomp.AttributeDenotation[];

     (* deliver *)
     l2dcomp[]->l2ast[]
  #);

gMergeCase: gBeta2abetaPrefix
  (# name::(# do 'Merge'->value[] #);
     node: ^Merge
  do 'Merge'->unexpected
     (* done in ObjectDenotation, StructureReference,
      * DynamicItemGeneration, and DynamicComponentGeneration *)
  #);

gNamedIndexCase: gBeta2abetaPrefix
  (# name::(# do 'NamedIndex'->value[] #);
     node: ^NamedIndex;
     l2ninx: ^interpreterbase.l2NamedIndex
  do mpsAst[]->node[];
     (&ib.l2NamedIndex[]).init->l2ninx[];
     (node.getNameDcl,l2ninx.theDeclNode[])
       ->asl2_NameDcl
       ->l2ninx.NameDcl[];
     (focus[],node.getEvaluation,Evaluation##)
       ->astOrSlotToLevelTwo
       ->l2ninx.Evaluation[];
     l2ninx[]->l2ast[]
  #);

gActionPartCase: gBeta2abetaPrefix
  (# name::(# do 'ActionPart'->value[] #)
  do 'ActionPart'->unexpected
     (* done in ObjectDescriptor *)
  #);

gEnterPartCase: gBeta2abetaPrefix
  (# name::(# do 'EnterPart'->value[] #)
  do 'EnterPart'->unexpected
     (* done in ObjectDescriptor *)
  #);

gDoPartCase: gBeta2abetaPrefix
  (# name::(# do 'DoPart'->value[] #)
  do 'DoPart'->unexpected
     (* done in ObjectDescriptor *)
  #);

gExitPartCase: gBeta2abetaPrefix
  (# name::(# do 'ExitPart'->value[] #)
  do 'ExitPart'->unexpected
     (* done in ObjectDescriptor *)
  #);

gImperativesCase: gBeta2abetaPrefix
  (# name::(# do 'Imperatives'->value[] #)
  do 'Imperatives'->unexpected
     (* done in MainPart,ForImp,SimpleIfImp,Alternative,ElsePart *)
  #);

gLabelledImpCase: gBeta2abetaPrefix
  (# name::(# do 'LabelledImp'->value[] #);
     node: ^LabelledImp;
     l2limp: ^interpreterbase.l2LabelledImp
  do mpsAst[]->node[];
     (&ib.l2LabelledImp[]).init->l2limp[];
     (node.getNameDcl,l2limp.theDeclNode[])
       ->asl2_NameDcl
       ->l2limp.NameDcl[];
     (* 1->l2limp.NameDcl.offset; to be compatible with ordinary part obj.s *)
     (focus[],node.getImp,Imp##)
       ->astOrSlotToLevelTwo
       ->l2limp.Imp[];
     l2limp[]->l2ast[]
  #);

gForImpCase: gBeta2abetaPrefix
  (# name::(# do 'ForImp'->value[] #);
     node: ^ForImp;
     theL2Index: ^interpreterbase.l2AstNode
  do mpsAst[]->node[];
     (focus[],node.getIndex,Index##)
       ->astOrSlotToLevelTwo
       ->theL2Index[];

     (* distinguish between for with/w.out named index *)
     (if true
      // theL2Index##<=ib.l2SimpleIndex## then
         (# l2sfimp: ^interpreterbase.l2SimpleForImp
         do (&ib.l2SimpleForImp[]).init->l2sfimp[];
            theL2Index[]->l2sfimp.SimpleIndex[];
            (focus[],node.getImperatives,l2sfimp.Imperatives[],
            Imperatives##,Imp##)
              ->astListOrOneSlotToLevelTwo;
            l2sfimp[]->l2ast[]
         #)
      // theL2Index##<=ib.l2NamedIndex## then
         (# l2nfimp: ^interpreterbase.l2NamedForImp
         do (&ib.l2NamedForImp[]).init->l2nfimp[];
            theL2Index[]->l2nfimp.NamedIndex[];
            (focus[],node.getImperatives,l2nfimp.Imperatives[],
            Imperatives##,Imp##)
              ->astListOrOneSlotToLevelTwo;
            l2nfimp[]->l2ast[]
         #)
     if)
  #);

gWhileImpCase: gBeta2abetaPrefix
  (# name::(# do 'WhileImp'->value[] #);
     node: ^WhileImp;
     l2wimp: ^interpreterbase.l2WhileImp

  do mpsAst[]->node[];
     (&ib.l2WhileImp[]).init->l2wimp[];
     (focus[],node.getEvaluation,Evaluation##)
       ->astOrSlotToLevelTwo
       ->l2wimp.Evaluation[];
     (focus[],node.getImperatives,l2wimp.Imperatives[],Imperatives##,Imp##)
       ->astListOrOneSlotToLevelTwo;
     l2wimp[]->l2ast[]
  #);

gGeneralIfImpCase: gBeta2abetaPrefix
  (# name::(# do 'GeneralIfImp'->value[] #);
     node: ^GeneralIfImp;
     maybeElsePart: ^ast;
     theElsePart: ^ElsePart;
     l2gif: ^interpreterbase.l2GeneralIfImp

  do mpsAst[]->node[];
     (&ib.l2GeneralIfImp[]).init->l2gif[];
     (focus[],node.getEvaluation,Evaluation##)
       ->astOrSlotToLevelTwo
       ->l2gif.Evaluation[];
     (focus[],node.getAlternatives,l2gif.Alternatives[]
     ,Alternatives##,Alternative##)
       ->astListOrSlotsToLevelTwo;
     node.getElsePartOpt->maybeElsePart[];
     (if maybeElsePart.symbol=ElsePart then
         maybeElsePart[]->theElsePart[];
         (focus[],theElsePart.getImperatives,l2gif.ElsePartOpt[],
         Imperatives##,Imp##)
           ->astListOrOneSlotToLevelTwo
     if);
     l2gif[]->l2ast[]
  #);

gSimpleIfImpCase: gBeta2abetaPrefix
  (# name::(# do 'SimpleIfImp'->value[] #);
     node: ^SimpleIfImp;
     maybeElsePart: ^ast;
     theElsePart: ^ElsePart;
     l2sif: ^interpreterbase.l2SimpleIfImp

  do mpsAst[]->node[];
     (&ib.l2SimpleIfImp[]).init->l2sif[];
     (focus[],node.getEvaluation,Evaluation##)
       ->astOrSlotToLevelTwo
       ->l2sif.Evaluation[];
     (focus[],node.getImperatives,l2sif.Imperatives[],Imperatives##,Imp##)
       ->astListOrOneSlotToLevelTwo;
     node.getElsePartOpt->maybeElsePart[];
     (if maybeElsePart.symbol=ElsePart then
         maybeElsePart[]->theElsePart[];
         (focus[],theElsePart.getImperatives,l2sif.ElsePartOpt[],
         Imperatives##,Imp##)
           ->astListOrOneSlotToLevelTwo
     if);
     l2sif[]->l2ast[]
  #);

gWhenImpCase: gBeta2abetaPrefix
  (# name::(# do 'WhenImp'->value[] #);
     node: ^WhenImp;
     maybeElsePart: ^ast;
     theElsePart: ^ElsePart;
     l2when: ^interpreterbase.l2WhenImp

  do mpsAst[]->node[];

     (* extract the namedcl of the target entity of the typecasing *)
     (&ib.l2WhenImp[]).init->l2when[];
     (node.getNameDcl,l2when.theDeclNode[])
       ->asl2_NameDcl
       ->l2when.NameDcl[];

     (* extract the a.den. which selects the target *)
     (focus[],node.getAttributeDenotation,AttributeDenotation##)
       ->astOrSlotToLevelTwo
       ->l2when.AttributeDenotation[];

     (* extract the cases, then set up the ghost name dcls *)
     (focus[],node.getWhenAlternatives,l2when.WhenAlternatives[]
     ,WhenAlternatives##,WhenAlternative##)
       ->astListOrSlotsToLevelTwo;
     l2when.WhenAlternatives.scan
     (# do current.ghostNameDcl.init;
        l2when.NameDcl.value->current.ghostNameDcl.value;
        l2when.NameDcl.mpsAst[]->current.ghostNameDcl.mpsAst[];
        current.theDeclNode[]->current.ghostNameDcl.decl[];
        current.theScopeNode[]->current.ghostNameDcl.scope[];
        current[]->current.ghostNameDcl.father[]
     #);

     (* extract the imperatives in the else-part *)
     node.getElsePartOpt->maybeElsePart[];
     (if maybeElsePart.symbol=ElsePart then
         maybeElsePart[]->theElsePart[];
         (focus[],theElsePart.getImperatives,l2when.ElsePartOpt[],
         Imperatives##,Imp##)
           ->astListOrOneSlotToLevelTwo
     if);

     (* deliver *)
     l2when[]->l2ast[]
  #);

gLeaveImpCase: gBeta2abetaPrefix
  (# name::(# do 'LeaveImp'->value[] #);
     node: ^LeaveImp;
     l2limp: ^interpreterbase.l2LeaveImp
  do mpsAst[]->node[];
     (&ib.l2LeaveImp[]).init->l2limp[];
     node.getScopeSpecOpt->handleScopeSpec
     (# onNameApl::(# do l2napl[]->l2limp.NameAplOpt[] #);
        onCount::(# do true->l2limp.UsesCount; value->l2limp.CountOpt #);
        onDefault::(# do false->l2limp.UsesCount; 1->l2limp.CountOpt #)
     #);
     l2limp[]->l2ast[]
  #);

gRestartImpCase: gBeta2abetaPrefix
  (# name::(# do 'RestartImp'->value[] #);
     node: ^RestartImp;
     l2rimp: ^interpreterbase.l2RestartImp
  do mpsAst[]->node[];
     (&ib.l2RestartImp[]).init->l2rimp[];
     node.getScopeSpecOpt->handleScopeSpec
     (# onNameApl::(# do l2napl[]->l2rimp.NameAplOpt[] #);
        onCount::(# do true->l2rimp.UsesCount; value->l2rimp.CountOpt #);
        onDefault::(# do false->l2rimp.UsesCount; 1->l2rimp.CountOpt #)
     #);
     l2rimp[]->l2ast[]
  #);

gInnerImpCase: gBeta2abetaPrefix
  (# name::(# do 'InnerImp'->value[] #);
     node: ^InnerImp;
     maybeScopeSpec: ^ast;
     l2iimp: ^interpreterbase.l2InnerImp
  do mpsAst[]->node[];
     (&ib.l2InnerImp[]).init->l2iimp[];
     node.getScopeSpecOpt->handleScopeSpec
     (# onNameApl::(# do l2napl[]->l2iimp.NameAplOpt[] #);
        onCount::(# do true->l2iimp.UsesCount; value->l2iimp.CountOpt #);
        onDefault::(# do false->l2iimp.UsesCount; 1->l2iimp.CountOpt #)
     #);
     l2iimp[]->l2ast[]
  #);

gSuspendImpCase: gBeta2abetaPrefix
  (# name::(# do 'SuspendImp'->value[] #)
  do (&ib.l2SuspendImp[]).init->l2ast[]
  #);

gCodeImpCase: gBeta2abetaPrefix
  (# name::(# do 'CodeImp'->value[] #)
  do 'CodeImp'->unsupported
  #);

gAlternativesCase: gBeta2abetaPrefix
  (# name::(# do 'Alternatives'->value[] #)
  do 'Alternatives'->unexpected
     (* done in GeneralIfImp *)
  #);

gAlternativeCase: gBeta2abetaPrefix
  (# name::(# do 'Alternative'->value[] #);
     node: ^Alternative;
     l2alt: ^interpreterbase.l2Alternative
  do mpsAst[]->node[];
     (&ib.l2Alternative[]).init->l2alt[];
     (focus[],node.getSelections,l2alt.Selections[],Selections##,Selection##)
       ->astListOrOneSlotToLevelTwo;
     (focus[],node.getImperatives,l2alt.Imperatives[],Imperatives##,Imp##)
       ->astListOrOneSlotToLevelTwo;
     l2alt[]->l2ast[]
  #);

gSelectionsCase: gBeta2abetaPrefix
  (# name::(# do 'Selections'->value[] #)
  do 'Selections'->unexpected
     (* done in Alternative *)
  #);

gCaseSelectionCase: gBeta2abetaPrefix
  (# name::(# do 'CaseSelection'->value[] #);
     node: ^CaseSelection
  do mpsAst[]->node[];
     (focus[],node.getEvaluation,Evaluation##)
       ->astOrSlotToLevelTwo
       ->l2ast[]
  #);

gWhenAlternativesCase: gBeta2abetaPrefix
  (# name::(# do 'WhenAlternatives'->value[] #)
  do 'WhenAlternatives'->unexpected
     (* done in WhenImp *)
  #);

gWhenAlternativeCase: gBeta2abetaPrefix
  (# name::(# do 'WhenAlternative'->value[] #);
     node: ^WhenAlternative;
     maybeExact: ^ast;
     ignoreDclNode: ^fragmentGraph.node;
     l2walt: ^interpreterbase.l2WhenAlternative
  do mpsAst[]->node[];
     (&ib.l2WhenAlternative[]).init->l2walt[];

     (* check whether this is an exact match *)
     (focus[],node.getExactOpt,Exact##)
       ->astOrSlotOrMissingToAst
       ->(ignoreDclNode[],maybeExact[]);
     (maybeExact[]<>NONE)
       ->l2walt.Exact;

     (* extract the attribute denotation *)
     (focus[],node.getAttributeDenotation,AttributeDenotation##)
       ->astOrSlotToLevelTwo
       ->l2walt.AttributeDenotation[];

     (* get the list of imperatives *)
     (focus[],node.getImperatives,l2walt.Imperatives[],Imperatives##,Imp##)
       ->astListOrOneSlotToLevelTwo;

     (* deliver *)
     l2walt[]->l2ast[]
  #);

gElsePartCase: gBeta2abetaPrefix
  (# name::(# do 'ElsePart'->value[] #)
  do 'ElsePart'->unexpected
     (* done in GeneralIfImp,SimpleIfImp *)
  #);

(*
 * local variables:
 * ee-beta-main-file: "~/gbeta-0.9/src/main/gbeta.bet"
 * end:
 *)
